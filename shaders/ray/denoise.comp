#version 460
#extension GL_GOOGLE_include_directive : require
#include "../utils/constants.glsl"
#include "pixel_buffer.glsl"

layout(binding = 0, set = 0) readonly buffer Pixel{ pixel_buffer_t in_pixel[]; };

layout(binding = 1, rgba32f) writeonly uniform image2D u_out_image;

layout(binding = 2, set = 0) writeonly buffer Depth{ float out_depth[]; };

layout(binding = 3, r16ui) writeonly uniform uimage2D out_object_ids;

struct push_constants_t
{
    uvec2 size;
    bool denoise;
};

layout(push_constant) uniform PushConstant{push_constants_t constants;};

layout(local_size_x = 16, local_size_y = 16) in;

//! edge-avoiding Ã€-TrousWavelet for denoising
vec4 denoise(ivec2 coords)
{
    const float kernel[25] = float[25](
    1.0/256.0, 1.0/64.0, 3.0/128.0, 1.0/64.0, 1.0/256.0,
    1.0/64.0,  1.0/16.0, 3.0/32.0,  1.0/16.0, 1.0/64.0,
    3.0/128.0, 3.0/32.0, 9.0/64.0,  3.0/32.0, 3.0/128.0,
    1.0/64.0,  1.0/16.0, 3.0/32.0,  1.0/16.0, 1.0/64.0,
    1.0/256.0, 1.0/64.0, 3.0/128.0, 1.0/64.0, 1.0/256.0 );

    const ivec2 offset[25] = ivec2[25](
    ivec2(-2,-2), ivec2(-1,-2), ivec2(0,-2), ivec2(1,-2), ivec2(2,-2),
    ivec2(-2,-1), ivec2(-1,-1), ivec2(0,-2), ivec2(1,-1), ivec2(2,-1),
    ivec2(-2, 0), ivec2(-1, 0), ivec2(0, 0), ivec2(1, 0), ivec2(2, 0),
    ivec2(-2, 1), ivec2(-1, 1), ivec2(0, 1), ivec2(1, 1), ivec2(2, 1),
    ivec2(-2, 2), ivec2(-1, 2), ivec2(0, 2), ivec2(1, 2), ivec2(2, 2) );

    vec2 fragCoord = coords;
    vec3 sum = vec3(0.0);
    float colorPhi = 1.0 / .9f; //ubo.colorPhi;
    float normalPhi = 1.0 / .3f; // ubo.normalPhi;
    float positionPhi = 1.0 / .6f; //ubo.positionPhi;

    uint buffer_idx = coords.y * constants.size.x + coords.x;
    vec3 cval = in_pixel[buffer_idx].radiance.rgb;//imageLoad(u_input[COLOR], coords).rgb;     // color
    vec3 nval = octahedral_mapping_to_normalized_vector(unpack_snorm_2x16(in_pixel[buffer_idx].normal));   // normal
//    vec3 pval = imageLoad(u_input[POSITIONS], coords).rgb; // position

    float cum_w = 0.0;

    for(uint i = 0; i < 25; ++i)
    {
        ivec2 uv = clamp(ivec2(fragCoord + offset[i] * 2), ivec2(0), ivec2(constants.size - 1));
        buffer_idx = uv.y * constants.size.x + uv.x;

        // Color
        vec3 ctmp = in_pixel[buffer_idx].radiance.rgb;
        vec3 t = cval - ctmp;							// Ip - Iq		(color difference)
        float dist2 = dot(t, t);						// ||Ip - Iq||	(distance squared)
        float c_w = min(exp(-(dist2) * colorPhi), 1.0); // w(p,q)		(weight function)

        vec3 ntmp = octahedral_mapping_to_normalized_vector(unpack_snorm_2x16(in_pixel[buffer_idx].normal));//imageLoad(u_input[NORMALS], uv).rgb;
        t = nval - ntmp;
        dist2 = dot(t, t);
        float n_w = min(exp(-(dist2) * normalPhi), 1.0);

//        vec3 ptmp = imageLoad(u_input[POSITIONS], uv).rgb;
//        t = pval - ptmp;
//        dist2 = dot(t, t);
        float p_w = 1.0;//min(exp(-(dist2) * positionPhi), 1.0);

        float weight = c_w * n_w * p_w * kernel[i];
        sum += ctmp * weight;
        cum_w += weight;
    }
    return vec4(sum / cum_w, 0.f);
}

void main()
{
    ivec2 coords = ivec2(gl_GlobalInvocationID.xy);

    uint buffer_idx = gl_GlobalInvocationID.y * constants.size.x + gl_GlobalInvocationID.x;
    out_depth[buffer_idx] = in_pixel[buffer_idx].depth;

    if(constants.denoise)
    {
        imageStore(u_out_image, coords, denoise(coords));
    }
    else
    {
        // store radiance
        imageStore(u_out_image, coords, vec4(in_pixel[buffer_idx].radiance, in_pixel[buffer_idx].alpha));
    }

    // store object-ids
    imageStore(out_object_ids, coords, uvec4(MAX_UINT16 - in_pixel[buffer_idx].object_id));
}