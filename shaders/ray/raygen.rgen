#version 460
#extension GL_EXT_ray_tracing : enable
#extension GL_GOOGLE_include_directive : enable

#include "ray_common.glsl"

layout(binding = 0, set = 0) uniform accelerationStructureEXT topLevelAS;
layout(binding = 1, set = 0, rgba32f) uniform image2D image;
layout(binding = 2, set = 0) uniform CameraProperties
{
    mat4 viewInverse;
    mat4 projInverse;
} cam;

layout(push_constant) uniform PushConstants
{
    push_constants_t push_constants;
};

layout(location = 0) rayPayloadEXT payload_t payload;

vec3 trace(vec2 coord, vec2 size)
{
    // State of the random number generator.
    uint rngState = rng_seed(push_constants);

    vec3 camera_position = (cam.viewInverse * vec4(0, 0, 0, 1)).xyz;

    // The sum of the colors of all of the samples.
    vec3 radiance = vec3(0);

    // Limit the kernel to trace at most 64 samples.
    const int NUM_SAMPLES = 8;

    float sample_offset = rng_float(rngState);

    // sample random dirctions around the pixel's center
    //           1
    //    .------+------.
    //    |      |      |
    // -a + ---- 0 ---- + a
    //    |      |      |
    //    '------+------'
    //          -1
    for (int sampleIdx = 0; sampleIdx < NUM_SAMPLES; sampleIdx++)
    {
        // random+stratified sampling
        const vec2 randomPixelCenter = coord + fract(Hammersley(sampleIdx, NUM_SAMPLES) + vec2(sample_offset));
        const vec2 inUV = randomPixelCenter / size;
        vec2 d = inUV * 2.0 - 1.0;

        // ray direction
        vec3 target = (cam.projInverse * vec4(d.x, d.y, 1, 1)).xyz;
        vec3 direction = (cam.viewInverse * vec4(normalize(target.xyz), 0)).xyz;

        // next ray
        Ray ray = Ray(camera_position, direction);

        Cone cone = Cone(0, 0);

        float tmin = 0.0001;
        float tmax = 10000.0;

        const uint max_depth = 3;

        vec3 beta = vec3(1);
        vec3 path_radiance = vec3(0);

        // trace a path
        for (int i = 0; i < max_depth; i++)
        {
            payload.stop = false;
            payload.radiance = path_radiance;
            payload.beta = beta;
            payload.ray = ray;
            payload.cone = cone;

            const uint ray_flags = gl_RayFlagsOpaqueEXT | gl_RayFlagsCullBackFacingTrianglesEXT;

            // trace one ray
            traceRayEXT(topLevelAS,     // acceleration structure
                        ray_flags,      // rayflags
                        0xff,           // cullMask
                        0,              // sbtRecordOffset
                        0,              // sbtRecordStride
                        0,              // missIndex
                        ray.origin,     // ray origin
                        tmin,           // ray min range
                        ray.direction,  // ray direction
                        tmax,           // ray max range
                        0);             // payload (location = 0)

            ray = payload.ray;
            cone = payload.cone;
            beta = payload.beta;
            path_radiance = payload.radiance;

            // nothing hit, end recursion
            if (payload.stop){ break; }
        }

        radiance += path_radiance;
    }
    return radiance / NUM_SAMPLES;
}

void main()
{
    vec3 color = trace(gl_LaunchIDEXT.xy, gl_LaunchSizeEXT.xy);

    if (push_constants.batch_index != 0)
    {
        uint size = push_constants.batch_index;
        color = (size * imageLoad(image, ivec2(gl_LaunchIDEXT.xy)).rgb + color) / (size + 1);
    }

    imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(color, 1.0));
}
