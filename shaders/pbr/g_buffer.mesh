#version 460
#extension GL_EXT_mesh_shader : require
#extension GL_GOOGLE_include_directive : require
#extension GL_EXT_scalar_block_layout : require
#extension GL_EXT_shader_8bit_storage: require
#extension GL_EXT_shader_16bit_storage: require

#extension GL_EXT_shader_explicit_arithmetic_types: require

#include "../renderer/types.glsl"
#include "../renderer/packed_vertex.glsl"
#include "../renderer/mesh_task_payload.glsl"
#include "../utils/camera.glsl"

layout(local_size_x = MESH_WORKGROUP_SIZE, local_size_y = 1, local_size_z = 1) in;
layout(triangles, max_vertices = 64, max_primitives = 64) out;

layout(set = 0, binding = BINDING_VERTICES, scalar) readonly buffer VertexBuffer
{
    packed_vertex_t vertices[];
};

layout(set = 0, binding = BINDING_MESHLETS) readonly buffer MeshletBuffer
{
    meshlet_t meshlets[];
};

layout(set = 0, binding = BINDING_MESHLET_VERTICES) readonly buffer MeshletVertexBuffer
{
    uint meshlet_vertices[];
};

layout(set = 0, binding = BINDING_MESHLET_TRIANGLES) readonly buffer MeshletTriangleBuffer
{
    uint8_t meshlet_triangles[];
};

layout(std140, set = 0, binding = BINDING_MESH_DRAWS) readonly buffer MeshDrawBuffer
{
    mesh_draw_t draws[];
};

layout(std140, binding = BINDING_JITTER_OFFSET) uniform UBOJitter
{
    camera_t camera;
    camera_t last_camera;
};

layout(push_constant) uniform PushConstants
{
    render_context_t context;
};

taskPayloadSharedEXT mesh_task_payload_t task_payload;

layout(location = LOCATION_INDEX_BUNDLE) flat out index_bundle_t indices[];
layout(location = LOCATION_VERTEX_BUNDLE) out VertexData
{
    vec2 tex_coord;
    vec3 normal;
    vec3 tangent;
    vec4 current_position;
    vec4 last_position;
} vertex_out[];

void main()
{
    uint ti = gl_LocalInvocationID.x;
    uint mi = task_payload.meshlet_base_index + uint(task_payload.meshlet_delta_indices[gl_WorkGroupID.x]);
    SetMeshOutputsEXT(meshlets[mi].vertex_count, meshlets[mi].triangle_count);

    matrix_struct_t m = draws[task_payload.object_index].current_matrices;
    matrix_struct_t m_last = draws[task_payload.object_index].last_matrices;

    //    //! would work when MESH_WORKGROUP_SIZE >= max_vertices -> iff >=1 thread per vertex
    //    if(ti < meshlets[mi].vertex_count)
    for(uint i = ti; i < meshlets[mi].vertex_count; i += MESH_WORKGROUP_SIZE)
    {
        uint vi = meshlet_vertices[meshlets[mi].vertex_offset + i];
        const Vertex v = unpack(vertices[vi]);

        vertex_out[i].current_position = camera.projection * camera.view * m.modelview * vec4(v.position, 1.0);
        vertex_out[i].last_position = last_camera.projection * last_camera.view * m_last.modelview * vec4(v.position, 1.0);

        vec4 jittered_position = vertex_out[i].current_position;
        jittered_position.xy += 2.0 * camera.sample_offset * jittered_position.w;
        gl_MeshVerticesEXT[i].gl_Position = jittered_position;

        indices[i].mesh_draw_index = task_payload.object_index;
        indices[i].material_index = draws[task_payload.object_index].material_index;
        indices[i].meshlet_index = mi;

        vertex_out[i].tex_coord = (m.texture * vec4(v.tex_coord, 0, 1)).xy;
        vertex_out[i].normal = normalize(mat3(m.normal) * v.normal);
        vertex_out[i].tangent = normalize(mat3(m.normal) * v.tangent);
    }

    for(uint i = ti; i < meshlets[mi].triangle_count; i += MESH_WORKGROUP_SIZE)
//    if(ti < meshlets[mi].triangle_count)
    {
        uint base_index = meshlets[mi].triangle_offset + 3 * i;
        gl_PrimitiveTriangleIndicesEXT[i] = uvec3(uint(meshlet_triangles[base_index]),
                                                  uint(meshlet_triangles[base_index + 1]),
                                                  uint(meshlet_triangles[base_index + 2]));

        // TODO: check if this is a validation-bug:
        // "The SPIR-V Capability (FragmentShadingRateKHR) was declared, but none of the requirements were met to use it"
        //! triangle culling hook
//        gl_MeshPrimitivesEXT[i].gl_CullPrimitiveEXT = bool(i % 2);
    }
}